////////////////////////////////////////////////////////////////////////////////
//
// Code::Blocks new project wizard script
//
// Project: Console application
// Author:  Yiannis Mandravellos
//
//
// NOTE: This is a *very* simple wizard script, but it's fully commented so you
// know what's going on :)
//
////////////////////////////////////////////////////////////////////////////////


// There is one global variable implicitely defined (exported from Code::Blocks)
// It is named "Wizard". It represents the wizard dialog object and contains
// the following methods:
//
//    function AddIntroPage(const wxString& in)
//    function AddProjectPathPage()
//    function AddCompilerPage(const wxString& in)
//    function AddPage(const wxString& in)
//
//    function SetTextControlValue(const wxString& in, const wxString& in)
//    wxString GetTextControlValue(const wxString& in)
//
//    function CheckCheckbox(const wxString& in, bool)
//    bool IsCheckboxChecked(const wxString& in)
//    function FillComboboxWithCompilers(const wxString& in)
//    wxString GetCompilerFromCombobox(const wxString& in)
//    wxString GetComboboxStringSelection(const wxString& in)
//    int GetComboboxSelection(const wxString& in)
//    function SetComboboxSelection(const wxString& in,int)
//
//    int GetRadioboxSelection(const wxString& in)
//    function SetRadioboxSelection(const wxString& in,int)
//
//    wxString& GetTemplatePath()
//    wxString GetProjectPath()
//    wxString GetProjectName()
//    wxString GetCompilerID()
//
//    bool GetWantDebug()
//    wxString GetDebugName()
//    wxString GetDebugOutputDir()
//    wxString GetDebugObjectOutputDir()
//
//    bool GetWantRelease()
//    wxString GetReleaseName()
//    wxString GetReleaseOutputDir()
//    wxString GetReleaseObjectOutputDir()


// Global variables declaration.
//
// Don't over-react about using global variables. This script will be discarded
// once finished running, so no reason to panic ;)

//bool IsCpp; // hold the source type for the new file (C or C++)

// This is the very first script function that will be called by Code::Blocks.
// The script should add all its wizard pages here.
// Pages will appear in the order they 're added.
function BeginWizard()
{
    // this is the text that will appear in the start (intro) page
    local intro_msg = _T("Welcome to the new console application wizard!\n"
                        "This wizard will guide you to create a new console application.\n\n"
                        "When you 're ready to proceed, please click \"Next\"...");

    // add builtin pages
	
	// intro
	Wizard.AddIntroPage(intro_msg);
	// select project name and path
	Wizard.AddProjectPathPage();
	// select compiler and configurations
	// args:
	//  1) string: which compiler ID will be pre-selected (empty means default)
	//  2) string: semi-colon separated list of allowed compiler ids. Use _T("*") for all. * and ? wildcards allowed.
	//  3) boolean: if false, the compiler selection will be disabled
	//  4) boolean: if false, the config targets will be disabled
	Wizard.AddCompilerPage(_T(""), _T("*"), true, true);
	// select language
	Wizard.AddLanguagePage(_T("C;C++"), 1);

    // add custom pages, present in the XRC (wizard.xrc)
    // each page must exist as a wxPanel with the same name in the XRC.
//	Wizard.AddPage(_T("SourceType")); // select source file type (C or C++)

	// nothing more needs to be done here
}

// There are three wizard events provided for custom pages (from XRC):
//
//  1) function OnEnter_<panel_name>(bool fwd)
//      This is called when the wizard is about to display this page.
//      The _T("fwd") argument is _T("true") if the page is displayed after the user pressed _T("Next"),
//      or it is _T("false") if the user pressed _T("Back").
//
//  2) bool OnLeave_<panel_name>(bool fwd)
//      This is called when the wizard is about to display another page.
//      The argument means the same as in OnEnter_<panel_name> above.
//      If the script returns _T("false") from this function, then the page-change
//      isn't allowed.
//
//  3) OnClick_<button_name>()
//      This is called when a button with the same name is clicked.

////////////////////////////////////////////////////////////////////////////////
// Source type's page
////////////////////////////////////////////////////////////////////////////////

//function OnLeave_SourceType(fwd)
//{
//	if (fwd)
//	{
//	    // if user pressed "Next", accept its source type settings
//	    // and keep it in the global variable we defined earlier.
//	    IsCpp = Wizard.GetRadioboxSelection(_T("rbLang")) == 0;
//	}
//	// allow changing this page
//	return true;
//}

////////////////////////////////////////////////////////////////////////////////
//
// The following functions are called by Code::Blocks after the wizard has
// finished running (user clicked "Finish").
//
////////////////////////////////////////////////////////////////////////////////

// Return the relative directory (to the templates path) that contains the files
// for the new project.
// This directory will be recursively scanned and every file and folder found
// will be copied by the wizard to the destination folder and will be added
// to the project.
// Note that you can return multiple directories by semicolon-separating them,
// e.g._T("console/common;console/cpp"). This way you can keep common files together
// and only separate different files based on the selected options.
function GetFilesDir()
{
    // depending on the source type setting, return the appropriate value.
    local lang = Wizard.GetLanguageIndex();
    local result;
    switch (lang)
    {
        case 0:
            // C source file
            result = _T("console/c");
            break;

        case 1:
        default:
            // C++ source file
            result = _T("console/cpp");
            break;
    }
    return result;
}

// Setup the already created project.
// By now, the project has already been created and it's time to set it up.
// The compiler ID has been already set, as well as the project title.
// Also, the requested targets (configurations) have been created and set up.
// Of course the script can now override anything ;)
// The "project" argument represents the new project.
function SetupProject(project)
{
    // NOTE: Major compiler system drawback here.
    // Until it is redesigned to allow easier compiler settings,
    // we have to check the compiler's ID and set options for different compilers...

    // enable compiler warnings (project-wide)
    WarningsOn(project, Wizard.GetCompilerID());

    // Debug build target
    local target = project.GetBuildTarget(Wizard.GetDebugName());
    if (!IsNull(target))
    {
        target.SetTargetType(ttConsoleOnly);
        //local s = Wizard.GetDebugOutputDir() + Wizard.GetProjectName() + DOT_EXT_EXECUTABLE;
        target.SetOutputFilename(Wizard.GetDebugOutputDir() + Wizard.GetProjectName() + DOT_EXT_EXECUTABLE);
        // enable debugging symbols for this target
        DebugSymbolsOn(target, Wizard.GetCompilerID());
    }

    // Release build target
    target = project.GetBuildTarget(Wizard.GetReleaseName());
    if (!IsNull(target))
    {
        target.SetTargetType(ttConsoleOnly);
        target.SetOutputFilename(Wizard.GetReleaseOutputDir() + Wizard.GetProjectName() + DOT_EXT_EXECUTABLE);
        // enable optimizations for this target
        OptimizationsOn(target, Wizard.GetCompilerID());
    }

    return true;
}
