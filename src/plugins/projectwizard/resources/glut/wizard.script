////////////////////////////////////////////////////////////////////////////////
//
// GLUT project wizard
//
////////////////////////////////////////////////////////////////////////////////

// globals
GlutPath <- _T("");

function BeginWizard()
{
    local intro_msg = _T("Welcome to the new GLUT project wizard!\n\n" +
                         "This wizard will guide you to create a new project\n" +
                         "using the GLUT OpenGL extensions.\n\n" +
                         "When you 're ready to proceed, please click \"Next\"...");

    Wizard.AddIntroPage(intro_msg);
    Wizard.AddProjectPathPage();
    Wizard.AddPage(_T("GlutPath"));
    Wizard.AddCompilerPage(_T(""), _T("*"), true, true);
}

////////////////////////////////////////////////////////////////////////////////
// GLUT's path page
////////////////////////////////////////////////////////////////////////////////

function OnEnter_GlutPath(fwd)
{
    if (fwd)
    {
        local dir = Wizard.GetTextControlValue(_T("txtGlutPath"));
        if (dir.length() == 0)
        {
            // first time here, set default value
            dir = ConfigManager.Read(_T("/glut_project_wizard/path"), _T(""));
            if (dir.IsEmpty()) // try global var
                dir = ReplaceMacros(_T("$(#glut)"), true);
            Wizard.SetTextControlValue(_T("txtGlutPath"), dir);
        }
    }
}

function OnLeave_GlutPath(fwd)
{
    if (fwd)
    {
        local dir = Wizard.GetTextControlValue(_T("txtGlutPath"));
        if (!IO.DirectoryExists(dir))
        {
            ShowError(_T("Please select a valid path to GLUT..."));
            return false;
        }
        if (!IO.FileExists(dir + _T("/include/GL/glut.h")))
        {
            ShowError(_T("The path you entered seems valid, but this wizard " +
                         "can't locate GLUT's files in it..."));
            return false;
        }

        // see if it matches the global var. if it does, use the var instead...
        local gvar = ReplaceMacros(_T("$(#glut)"), true);
        if (gvar.Matches(dir))
            dir = gvar;
        GlutPath = dir;
        ConfigManager.Write(_T("/glut_project_wizard/path"), dir, true);
    }
    return true;
}

function OnClick_btnGlutPathBrowse()
{
    local dir = Wizard.GetTextControlValue(_T("txtGlutPath"));
    dir = IO.SelectDirectory(_T("Please select GLUT's top-level folder"), dir, false);

    if (IO.DirectoryExists(dir))
        Wizard.SetTextControlValue(_T("txtGlutPath"), dir);
}

// return the files this project contains
function GetFilesDir()
{
    return _T("glut/files");
}

// setup the already created project
function SetupProject(project)
{
    // set project options
    
    // set compiler/linker search paths
    project.AddIncludeDir(GlutPath + _T("/include"));
    project.AddLibDir(GlutPath + _T("/lib"));

    // add link libraries
    if (PLATFORM == PLATFORM_MSW)
    {
        project.AddLinkLib(_T("glut32"));
        project.AddLinkLib(_T("opengl32"));
        project.AddLinkLib(_T("glu32"));
        project.AddLinkLib(_T("winmm"));
        project.AddLinkLib(_T("gdi32"));
    }
    else
    {
        project.AddLinkLib(_T("glut"));
        project.AddLinkLib(_T("GL"));
        project.AddLinkLib(_T("GLU"));
        project.AddLinkLib(_T("Xxf86vm"));
    }

    // enable compiler warnings (project-wide)
    WarningsOn(project, Wizard.GetCompilerID());

    // Debug
    local target = project.GetBuildTarget(Wizard.GetDebugName());
    if (!IsNull(target))
    {
        target.SetTargetType(ttConsoleOnly); // ttConsoleOnly: console for debugging
        target.SetOutputFilename(Wizard.GetDebugOutputDir() + Wizard.GetProjectName() + DOT_EXT_EXECUTABLE);
        target.SetWorkingDir(GlutPath + _T("/bin"));
        // enable generation of debugging symbols for target
        DebugSymbolsOn(target, Wizard.GetCompilerID());
    }

    // Release
    target = project.GetBuildTarget(Wizard.GetReleaseName());
    if (!IsNull(target))
    {
        target.SetTargetType(ttExecutable); // ttExecutable: no console
        target.SetOutputFilename(Wizard.GetReleaseOutputDir() + Wizard.GetProjectName() + DOT_EXT_EXECUTABLE);
        target.SetWorkingDir(GlutPath + _T("/bin"));
        // enable optimizations for target
        OptimizationsOn(target, Wizard.GetCompilerID());
    }

    return true;
}
