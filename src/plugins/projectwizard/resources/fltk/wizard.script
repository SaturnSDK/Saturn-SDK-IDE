////////////////////////////////////////////////////////////////////////////////
//
// FLTK project wizard
//
////////////////////////////////////////////////////////////////////////////////

// globals
FltkPath <- _T("");

function BeginWizard()
{
    local intro_msg = _T("Welcome to the new FLTK project wizard!\n\n"
                         "This wizard will guide you to create a new project\n"
                         "using the FLTK GUI C++ library.\n\n"
                         "When you 're ready to proceed, please click \"Next\"...");

    Wizard.AddIntroPage(intro_msg);
    Wizard.AddProjectPathPage();
    Wizard.AddPage(_T("FltkPath"));
    Wizard.AddCompilerPage(_T(""), _T("*"), true, true);
}

////////////////////////////////////////////////////////////////////////////////
// FLTK's path page
////////////////////////////////////////////////////////////////////////////////

function OnEnter_FltkPath(fwd)
{
    if (fwd)
    {
        local dir = Wizard.GetTextControlValue(_T("txtFltkPath"));
        if (dir.length() == 0)
        {
            // first time here, set default value
            dir = ConfigManager.Read(_T("/fltk_project_wizard/path"), _T(""));
            if (dir.IsEmpty()) // try global var
                dir = ReplaceMacros(_T("$(#fltk)"), true);
            Wizard.SetTextControlValue(_T("txtFltkPath"), dir);
        }
    }
}

function OnLeave_FltkPath(fwd)
{
    if (fwd)
    {
        local dir = Wizard.GetTextControlValue(_T("txtFltkPath"));
        if (!IO.DirectoryExists(dir))
        {
            ShowError(_T("Please select a valid path to FLTK..."));
            return false;
        }
        if (!IO.FileExists(dir + _T("/include/FL/Fl.H")))
        {
            ShowError(_T("The path you entered seems valid, but this wizard "
                         "can't locate FLTK's files in it..."));
            return false;
        }

        // see if it matches the global var. if it does, use the var instead...
        local gvar = ReplaceMacros(_T("$(#fltk)"), true);
        if (gvar.Matches(dir))
            dir = gvar;
        FltkPath = dir;
        ConfigManager.Write(_T("/fltk_project_wizard/path"), dir, true);
    }
    return true;
}

function OnClick_btnFltkPathBrowse()
{
    local dir = Wizard.GetTextControlValue(_T("txtFltkPath"));
    dir = IO.SelectDirectory(_T("Please select FLTK's top-level folder"), dir, false);

    if (IO.DirectoryExists(dir))
        Wizard.SetTextControlValue(_T("txtFltkPath"), dir);
}

// return the files this project contains
function GetFilesDir()
{
    return _T("fltk/files");
}

// setup the already created project
function SetupProject(project)
{
    // set project options

    // set compiler/linker search paths
    project.AddIncludeDir(FltkPath + _T("/include"));
    project.AddLibDir(FltkPath + _T("/lib"));

    // add link libraries
    project.AddLinkLib(_T("fltk"));
    project.AddLinkLib(_T("ole32"));
    project.AddLinkLib(_T("uuid"));
    project.AddLinkLib(_T("comctl32"));
    project.AddLinkLib(_T("wsock32"));
    project.AddLinkLib(_T("m"));
    project.AddLinkLib(_T("gdi32"));
    project.AddLinkLib(_T("user32"));
    project.AddLinkLib(_T("kernel32"));

    project.AddCompilerOption(_T("-DWIN32"))
    project.AddCompilerOption(_T("-mms-bitfields"))

    // Debug
    local target = project.GetBuildTarget(Wizard.GetDebugName());
    if (!IsNull(target))
    {
        target.SetTargetType(ttConsoleOnly); // ttConsoleOnly: console for debugging
        target.SetOutputFilename(Wizard.GetDebugOutputDir() + Wizard.GetProjectName() + DOT_EXT_EXECUTABLE);
        if (Wizard.GetCompilerID() == _T("gcc"))
        {
            // enable generation of debugging symbols for target
            // Note: DebugSymbolsOn() won't work because -Wall produces far too many warnings
            target.AddCompilerOption(_T("-g"));
        }
    }

    // Release
    target = project.GetBuildTarget(Wizard.GetReleaseName());
    if (!IsNull(target))
    {
        target.SetTargetType(ttExecutable); // ttExecutable: no console
        target.SetOutputFilename(Wizard.GetReleaseOutputDir() + Wizard.GetProjectName() + DOT_EXT_EXECUTABLE);
        if (Wizard.GetCompilerID() == _T("gcc"))
        {
            // enable optimizations for target.
            // Note: OptimizationsOn() won't work because of -I-!
            target.AddCompilerOption(_T("-O2"));
            target.AddCompilerOption(_T("-s"));
        }
    }

    return true;
}
